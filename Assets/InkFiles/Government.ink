== Government ==
~ FadeOutSeq("Jupiter is joy. / Jupiter is lavender. / Jupiter is love. / You are now playing as Almos.", "G1") 
->DONE 

== G1 ==
You feel yourself awaken in a stupor of amnesia. You have no idea why you are in an office. 
There's a computer screen before you that's now gone to sleep. You remember that you usually work late at night in a government building. 
You hope you weren't asleep for too long. In your wake, you feel a certain tinge of immateriality to the cubicle you now sit in.

You're most certainly in your office. You really hope you haven't accidentally slept for too long. 
There are diagrams scattered all over your desk, some of which have frantic and messy edits upon them chalked in with pencil. 
You try to open the drawer underneath your desk but the locks won't budge. You have no idea where the key could be.
You check the clock. Two hours left to go before you can go home. Your stomach bubbles. You head to the break room for food. 
The office is always empty. You suppose you just have an awkward shift, but it’s of your own accord. The hallways are dimly lit. On the way to the break room you pass several series of closed doors.  
All are locked by keycard. You always assume you don’t have the security privileges to enter them. There’s no real reason either, you just stick to your corner of the office. 
There’s a box of chocolate muffins in the break room. A little note is left in front of the box.
“Take one :) —Marilyn” 

You fucking love chocolate muffins.
You don’t really know who Marilyn is though. You put the muffin in the microwave to heat it up and watch intently as it whirs for a minute and fifteen seconds. 
You think about how Aiolos always says to not stand in front of microwaves. 
You think that’s bullshit. 

+ _Continue..._
    -> G2
    
== G2 ==
As you're sitting and eating the now gooey muffin, you hear a loud thud coming from the hall. 
The noise expands to the door of the room. 
You feel your heart lower and become heavy. 
You don't really want to stop eating the muffin, though, to investigate. The noise seems to cease and you reason that someone else just entered the office to work.
Minutes later you hear feet shuffling on the other side of the door once more. Someone starts clawing on the door.
“Um… The door isn’t locked. It’s open?” you mutter with both fear and confusion. 
The clawing turns to banging. The door swells with each strike. 
You feel electrified and numb. You feel like you can't breathe. You want to escape. 
You huddle into the corner with a butter knife from the table.

+ _Continue..._
    -> G3
    
== G3 ==
The door breaks open and you nearly faint. Your stomach can’t digest your beating heart and you fall to the floor in terror. Your vision becomes blurry. You can't move your legs. 

You claw your way back to the table to prop yourself on a chair. You get yourself upright and notice the door is in fact open. You don't think anyone entered the room. The room is dead.

+ "Hello?"
    -> G4
    
== G4 ==
"Hellooo!" A peculiar looking fellow peeks his head through the door. "Sorry I thought the door was sort of a pull... thing, not a push..."

He then bursts through the doorway and rushes to stand before you.

"BORIS IS HERE TO RESCUE YOU."

+ "Um... Hello Boris?"
    -> G5

+ "Hellooo Boris!"
    ~UncannyPeculiarity += 1
    -> GSpecial

== G5 ==
"WE NEED TO GET YOU BOTH OUT OF HERE." Boris's stance is now firm. 
* "Both? Who else is here?"
- "Yeah! Both! Aiolos is here too, you just can't see him. I've brought him here as requested.

* "As requested?"
- "Yes Mr. Echo-er. Get your own lines for once. Anyhoo, Of course you have, silly! Don't be stupid now. You've made this all a reality, and now is the time to escape back to Jupiter!

* "What is Jupiter?"
- "I'm sooo glade you asked!" Boris now shakes with excitement as if he's been waiting all his life to say the following: 

"Jupiter is multitude, NO, an AMALGAMATION of inexplicable feeling. 'THE PINACLE OF EXISTENCE' some might be bold enough to say." He pauses briefly to clear his throat, "It's also a very real place."

You stare blankly at Boris in both awe and confusion. 

"Well we aren't going the main way. We are using a sort of back-door per se. If you don't remember, this is your proxy world. Some data may have not been transferred to your current form."

* "How do you know all of this information?"
- "Well, you made me. I'm simply a vessel for all the information you had me contain."

Boris now reaches his arm around himself. He begins hugging himself. 

{MuffinShared == false:
"I feel so full, yet empty at the same time," Boris wallows. "If only I had that muffin."
}

You're not sure what to feel at this point. This is all quite bizarre to you. You wait in silence and continue to observe Boris hugging himself to the point where it becomes awkward.

* "So... What now?"
    -> G6

== G6 ==
Boris snaps out of his trance, "Anyhow, let's head to the control room, I'm meant to give the both of ya'll access so you can return to Jupiter. 
You won't be returning under traditional means, however, you both are prisoners after all. Just make sure you're well prepared for whatever awaits you on the other end."

You nervously look to Boris as he waddles out of the room. You follow sheepishly unknowing of what is to come. 

+ _Continue..._
    -> G7
    
== G7 ==
You follow Boris through corridors before he parks himself in front of a locked door. Boris reaches into his pocket and flips through a ring of keys in intense search. 
He finds the right key, unlocks the door and pushes through. You both are met with what seems to be a closet rather than a room. Within this confined space is a couple computer screens followed by a terminal at waist level. 

+ _Examine the terminal._
    -> ET
+ "You mentioned Aiolos being here as well?"
    -> G8
    
== G8 == 
"Yep!" Boris says. "Don't think too hard about that one. He's in another instance of your proxy world."

* "So are you in both instances at the same time?"
- "Well, yes," he said.
"I'm an overseer of  sorts --  a manager, if you will." 
You can't be bothered to quesetion this any further.

+ "Fine, let's just get out of here."
    -> G9
    
== G9 ==
"I thought you'd never ask!" Boris turns quickly to the terminal and begins furiously smashing the buttons. Suprisingly, it appears he is controlled and mechanical in his movements.

"Annnddd done!" Boris grabs hold of your hand and places it on a panel of the terminal. He moves his other hand and seemingly grabs the air and additionally places it on the terminal. "I'll see you on the other side, don't miss me for too long."

With that, your vision washes over, and you're being is succumbed to a red mist.

+ _Continue..._
    -> ParadiseLost

== ET  ==
You approach the terminal the terminal cautiously.
{UncannyPeculiarity > 2:
Your memory feels blurred, but you're slowly beggining to remember. This is an Andromeda terminal. It's meant to uphold simulations of grandeur and delusion. You worked on these exact models.
- else:
The thing whirs with seemingly redundant buttons flashing on that back off again. It gets to a point where it's a little dramatic, surely.
}

You notice a single keypad within the center of it.

"I see you eyeing the terminal," Boris chimes in. "This is how we're going to get you home."

You're not precisely sure what 'home' truly means. You don't know whether to trust Boris anymore. He doesn't seem to be particularly harmful, however. Well, at least to the extent in which he wouldn't harm himself. 

+ "You mentioned Aiolos being here as well?"
    -> G8

== GSpecial ==
"Hellooo," Boris echoes back. "Before we get down to business, can I have a piece of that muffin."

Boris is now indiscriminately pointing at the gooey muffin you left on the table in fear.

+ "Yes."
    ~ MuffinShared = true
    -> G1Special
+ "No."
    -> G1Special


== G1Special ==
{MuffinShared == true:
"Ah thanks man I was getting kind of hungry." 

He reaches for the muffin and stuffs the whole thing in his mouth. He looks to you now with his mouth full.

"Sorry I got a little carried away there," he says while crumbs are flying out of his mouth like sparks.

Your face goes sour with both confusion and the realization that your muffin is now gone. You look longingly at the table and mourn the loss briefly before redirecting your attention back to Boris. 
- else:
"Oh..." Boris pouts. "Ok then, I guess."

He still goes over to pick up the muffin and graceously hands it to you.

You skeptically take a couple bites out of it, half because you're still hungry, and half because you want to see the dissapointment on Boris' face when he realizes you truly aren't sharing. 

Boris watches intently as you finish the whole muffin right before him. He shakes off the dissapointment and his attention is brought back to you.
}

+ _Continue..._
    -> G5
    